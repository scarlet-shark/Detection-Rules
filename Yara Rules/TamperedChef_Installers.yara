import "pe"

rule TamperedChef_Installers {
  meta:
    description = "Detect fake application installers related to the Tampered Chef Campaign"
    author = "Acronis"
    version = "1"
    date = "2025-11-19"
    hash = "a16ecfcf5e6d7742f0e642309c3a0bf84eaf21962e663ce728f44c93ee70a28e"
    reference = "https://www.acronis.com/en/tru/posts/cooking-up-trouble-how-tamperedchef-uses-signed-apps-to-deliver-stealthy-payloads/"

  strings:
    // hex
    $a1 = {8D 55 EC B9 04 00 00 00 8B C7 E8 BA EC FF FF 8D 45 F8 33 C9 8B 55 EC E8 6D AF F6 FF 83 7D EC 00 74 14 8D 45 F8 E8 DF AC F6 FF 8B D0 8B 4D EC 8B C7 E8 93 EC FF FF 8B C6 8B 55 F8 E8 B9 AB F6 FF 83 C6 04 4B 75 BA}
    $a2 = {8D 45 ?8 50 [0-4] 6A 00 6A 00 6A 00 6A 00 6A 00 6A 00 8B 45 FC E8 33 8F F6 FF 50 6A 00 E8 13 F7 F6 FF 85 C0}
    $a3 = {8B 45 CC 8D 4D D0 BA DC 8A ?? 00 E8 88 32 FF FF 8B 45 D0 50 8D 55 C8 A1 04 42 ?? 00 E8 0F 32 FF FF}

    // strings
    $b1 = "1.0.0" wide
    $b2 = "CompanyName" wide
    $b3 = "Inno Setup" ascii wide
    $b4 = ".tmp" ascii wide

  condition:
    pe.is_pe
    and pe.number_of_sections > 10
    and pe.number_of_signatures > 0
    and for any i in (00 .. pe.number_of_signatures):
    (
        pe.signatures[i].issuer contains "Sectigo"
    )

    and filesize > 18MB
    and all of them
}
